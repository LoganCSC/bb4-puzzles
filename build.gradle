// Copyright by Barry G. Becker, 2013. Licensed under MIT License: http://www.opensource.org/licenses/MIT

apply from: 'https://raw.github.com/barrybecker4/bb4-common/master/bb4.gradle'

description = 'A library project containing a collection of heuristic optimization algorithms.'
group = 'com.barrybecker4'
version = '1.1.2-SNAPSHOT'
project.archivesBaseName = 'bb4-puzzle'
mainClassName = "com.barrybecker4.puzzle.maze.ui.MazeSimulator"

task runAdventure(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.adventure.ui.GraphicalAdventure'
    classpath = sourceSets.main.runtimeClasspath
}

task runTextAdventure(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.adventure.TextAdventure'
    classpath = sourceSets.main.runtimeClasspath
    args = ['ludlow/ludlowScript.xml']
}

task runMaze(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.maze.ui.MazeSimulator'
    classpath = sourceSets.main.runtimeClasspath
}

task runRedPuzzle(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.redpuzzle.ui.RedPuzzle'
    classpath = sourceSets.main.runtimeClasspath
}

task runTantrixPuzzle(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.tantrix.ui.TantrixPuzzle'
    classpath = sourceSets.main.runtimeClasspath
    //args = ['-name', 'galactic',  '-locale', 'ENGLISH']
}

task runSudoku(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.sudoku.ui.SudokuPuzzle'
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-ea"]
}

task runHiQ(type: JavaExec, depends: classes) {
    main = 'com.barrybecker4.puzzle.hiq.ui.HiQPuzzle'
    classpath = sourceSets.main.runtimeClasspath
    args = ['-locale', 'ENGLISH']
}


dependencies {
   compile 'com.barrybecker4:bb4-sound:1.1.2-SNAPSHOT'
   compile 'com.barrybecker4:bb4-optimization:1.1.2-SNAPSHOT'
}


task puzzleJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/common/**'
    baseName = 'bb4-puzzle'
    manifest {
        attributes 'Implementation-Title': 'Puzzle common code',
            'Implementation-Version': version,
            provider: 'gradle',
            'Main-Class': mainClassName
    }
}
task adventureJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/adventure/**'
    exclude 'com/barrybecker4/puzzle/adventure/**/*.pdf'
    baseName = 'bb4-adventure'
}
task hiqJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/hiq/**'
    baseName = 'bb4-hiq'
}
task mazeJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/maze/**'
    baseName = 'bb4-maze'
}
task redpuzzleJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/redpuzzle/**'
    baseName = 'bb4-redpuzzle'
}
task sudokuJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/sudoku/**'
    baseName = 'bb4-sudoku'
}
task tantrixJar(type: Jar) {
    from sourceSets.main.output
    include 'com/barrybecker4/puzzle/tantrix/**'
    baseName = 'bb4-tantrix'
}

artifacts {
    archives puzzleJar
    archives adventureJar
    archives hiqJar
    archives mazeJar
    archives redpuzzleJar
    archives sudokuJar
    archives tantrixJar

    archives sourcesJar
    archives javadocJar
}

jar {
    // skip building the default jar that contains everything
    enabled = false
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            //http://stackoverflow.com/questions/7826652/how-to-upload-an-existing-collection-of-3rd-party-jars-to-a-maven-server-in-grad
            //http://www.gradle.org/docs/current/userguide/userguide_single.html#sub:multiple_artifacts_per_project

            addFilter('bb4-puzzle') {artifact, file ->
                artifact.name == 'bb4-puzzle'
            }
            addFilter('bb4-hiq') {artifact, file ->
                artifact.name == 'bb4-hiq'
            }
            addFilter('bb4-maze') {artifact, file ->
                artifact.name == 'bb4-maze'
            }
            addFilter('bb4-redpuzzle') {artifact, file ->
                artifact.name == 'bb4-redpuzzle'
            }
            addFilter('bb4-sudoku') {artifact, file ->
                artifact.name == 'bb4-sudoku'
            }
            addFilter('bb4-tantrix') {artifact, file ->
                artifact.name == 'bb4-tantrix'
            }
            addFilter('bb4-adventure') {artifact, file ->
                artifact.name == 'bb4-adventure'
            }
        }
    }
}
